# Copyright 2019 The Bazel Authors. All rights reserved.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#    http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

load("//go/private:common.bzl", "GO_TOOLCHAIN_LABEL")
load(
    "//go/private:mode.bzl",
    "link_mode_arg",
)
load("//go/private/actions:utils.bzl", "path_mapping_action_settings", "quote_opts")

def _archive(v):
    importpaths = [v.data.importpath]
    importpaths.extend(v.data.importpath_aliases)
    return "{}={}={}".format(
        ":".join(importpaths),
        v.data.importmap,
        v.data.export_file.path if v.data.export_file else v.data.file.path,
    )

def _embedroot_arg(src):
    return src.root.path

def _embedlookupdir_arg(src):
    root_relative = src.dirname[len(src.root.path):]
    if root_relative.startswith("/"):
        root_relative = root_relative[1:]
    return root_relative

def emit_compilepkg(
        go,
        sources = None,
        cover = None,
        embedsrcs = [],
        importpath = "",
        importmap = "",
        archives = [],
        headers = depset(),
        cgo = False,
        cgo_inputs = depset(),
        cgo_out_dir = None,
        cppopts = [],
        copts = [],
        cxxopts = [],
        objcopts = [],
        objcxxopts = [],
        ldflags = None,
        out_lib = None,
        out_export = None,
        out_imports = None,
        out_cgo_export_h = None,
        gc_goopts = [],
        testfilter = None,  # TODO: remove when test action compiles packages
        recompile_internal_deps = [],
        is_external_pkg = False):
    """Compiles a complete Go package."""
    if sources == None:
        fail("sources is a required parameter")
    if out_lib == None:
        fail("out_lib is a required parameter")
    if out_imports == None:
        fail("out_imports is a required parameter")

    if cover and go.coverdata:
        archives = archives + [go.coverdata]

    sdk = go.sdk
    inputs_direct = (sources + embedsrcs + [sdk.package_list, go.toolchain._pack] +
                     [archive.data.export_file for archive in archives])
    inputs_transitive = [sdk.headers, sdk.tools, go.stdlib.libs, headers]
    outputs = [out_lib, out_export, out_imports]

    builder_args = go.builder_args(go)
    builder_args.add_all(sources, before_each = "-src")

    compile_args = go.tool_args(go)
    compile_args.add("-pack", go.toolchain._pack)
    compile_args.add_all(embedsrcs, before_each = "-embedsrc", expand_directories = False)
    compile_args.add_all(
        sources + [out_lib] + embedsrcs,
        map_each = _embedroot_arg,
        before_each = "-embedroot",
        uniquify = True,
        expand_directories = False,
    )
    compile_args.add_all(
        sources + [out_lib],
        map_each = _embedlookupdir_arg,
        before_each = "-embedlookupdir",
        uniquify = True,
        expand_directories = False,
    )

    if cover and go.coverdata:
        # Always use atomic mode as the "runtime/coverage" APIs require it.
        cover_mode = "atomic"
        builder_args.add("-cover_mode", cover_mode)
        compile_args.add("-cover_format", go.mode.cover_format)
        compile_args.add_all(cover, before_each = "-cover")

    builder_args.add_all(archives, before_each = "-arc", map_each = _archive)
    if recompile_internal_deps:
        builder_args.add_all(recompile_internal_deps, before_each = "-recompile_internal_deps")
    if importpath:
        builder_args.add("-importpath", importpath)
    else:
        builder_args.add("-importpath", go.label.name)
    if importmap:
        builder_args.add("-p", importmap)
    builder_args.add("-package_list", sdk.package_list)

    compile_args.add("-lo", out_lib)
    compile_args.add("-o", out_export)
    compile_args.add("-imports", out_imports)
    if out_cgo_export_h:
        compile_args.add("-cgoexport", out_cgo_export_h)
        outputs.append(out_cgo_export_h)
    if testfilter:
        builder_args.add("-testfilter", testfilter)

    link_mode_flag = link_mode_arg(go.mode)

    gc_flags = gc_goopts + go.mode.gc_goopts
    if go.mode.race:
        gc_flags.append("-race")
    if go.mode.msan:
        gc_flags.append("-msan")
    if go.mode.debug:
        gc_flags.extend(["-N", "-l"])
    gc_flags.extend(go.toolchain.flags.compile)
    if link_mode_flag:
        gc_flags.append(link_mode_flag)
    compile_args.add("-gcflags", quote_opts(gc_flags))

    if link_mode_flag:
        compile_args.add("-asmflags", link_mode_flag)

    # cgo and the linker action don't support path mapping yet
    # TODO: Remove the second condition after https://github.com/bazelbuild/bazel/pull/21921.
    env, execution_requirements = path_mapping_action_settings(go, cgo)
    cgo_go_srcs = None
    if cgo:
        if cgo_out_dir:
            cgo_go_srcs = cgo_out_dir
            outputs.append(cgo_go_srcs)
            compile_args.add("-cgo_go_srcs", cgo_go_srcs.path)
        inputs_transitive.append(cgo_inputs)
        inputs_transitive.append(go.cc_toolchain_files)
        env["CC"] = go.cgo_tools.c_compiler_path
        if cppopts:
            compile_args.add("-cppflags", quote_opts(cppopts))
        if copts:
            compile_args.add("-cflags", quote_opts(copts))
        if cxxopts:
            compile_args.add("-cxxflags", quote_opts(cxxopts))
        if objcopts:
            compile_args.add("-objcflags", quote_opts(objcopts))
        if objcxxopts:
            compile_args.add("-objcxxflags", quote_opts(objcxxopts))

    if go.mode.pgoprofile:
        compile_args.add("-pgoprofile", go.mode.pgoprofile)
        inputs_direct.append(go.mode.pgoprofile)

    arguments = ["compilepkg", builder_args, compile_args]
    if ldflags:
        arguments.append(ldflags)

    go.actions.run(
        inputs = depset(inputs_direct, transitive = inputs_transitive),
        outputs = outputs,
        mnemonic = "GoCompilePkgExternal" if is_external_pkg else "GoCompilePkg",
        executable = go.toolchain._builder,
        arguments = arguments,
        env = env,
        toolchain = GO_TOOLCHAIN_LABEL,
        execution_requirements = execution_requirements,
    )
